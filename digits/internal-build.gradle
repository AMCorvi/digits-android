/*
 * Copyright (C) 2015 Twitter, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply from: rootProject.qualityScript
apply from: rootProject.sizeScript
apply from: rootProject.deployScript
apply plugin: com.twitter.one.gradle.FabricPropertiesPlugin

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        versionCode 1
        versionName version
        minSdkVersion 8
        consumerProguardFiles 'proguard.txt'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""
            buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"
            // recommended fix from https://code.google.com/p/android/issues/detail?id=61573
            // triggered by including easymock 3.2 in testing/build.gradle
            packagingOptions {
                exclude 'META-INF/LICENSE'
                exclude 'META-INF/NOTICE'
                exclude 'META-INF/INDEX.LIST'
                exclude 'META-INF/jersey-module-version'
                exclude 'META-INF/ASL2.0'
                exclude 'LICENSE'
                exclude 'NOTICE'
                exclude 'asm-license.txt'
            }

            testCoverageEnabled enableTestCodeCoverage
        }
        release {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""
            buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"
        }
    }

    lintOptions {
        warningsAsErrors true
    }

    productFlavors {
    }

    jacoco {
        version = jacocoVersion
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    //Gradle publishes only release by default for multi module projects
    //This will publish all build variants and we can specifically choose them
    defaultPublishConfig 'release'
    publishNonDefault true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // Forcibly add debug and the generated "R" files onto the test classpath
    sourceSets {
        test.java.srcDirs += "build/generated/source/r/debug"
        test.java.srcDirs += "src/debug"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

ext.projectDependencies = [
        ':sdk:fabric:fabric',
        ':sdk:twitter:twitter-core'
]

dependencies {
    projectDependencies.each {
        debugCompile project(path: it, configuration: 'debug')
        releaseCompile kitDependency(path: it, configuration: 'release')
    }

    // We need to include the appcompat as a compile time dependency to workaround a bug that exists
    // in 1.1.0-1.1.3 version, see https://code.google.com/p/android/issues/detail?id=149268.
    compile rootProject.appCompat

    androidTestCompile rootProject.mockito
    androidTestCompile rootProject.dexmakerMockito
    androidTestCompile rootProject.androidTestingSupport

    testCompile rootProject.mockito
    testCompile rootProject.androidTestingSupport
    testCompile rootProject.junit
    testCompile(rootProject.roboelectric) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile (rootProject.junitAddons) {
        transitive = false
    }
}

